plugins {
    id 'java-library'
    id 'java'
    id 'org.springframework.boot' version '3.3.6' apply(false)
    id 'io.spring.dependency-management' version '1.1.6' apply(false)
    id 'org.asciidoctor.jvm.convert' version '3.3.2' apply(false)
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    ext {
        set('springCloudVer', "2023.0.3")
        set('querydsl.version', "5.1.0")
        set('redissonVersion', "3.27.1")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        asciidoctorExt
    }

    dependencies {
//        implementation 'org.springframework.boot:spring-boot-starter'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        // test
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'

        // RestDocs
        asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVer}"
        }
        dependencies {
            dependency "org.redisson:redisson-spring-boot-starter:${redissonVersion}"
        }
    }

    tasks.getByName("bootJar") {
        enabled = project.name == "lol-server-application"
    }

    tasks.getByName("jar") {
        enabled = project.name != "lol-server-application"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    bootRun {
        String activeProfile = System.properties['spring.profiles.active']
        systemProperty "spring.profiles.active", activeProfile
    }

    ext { // 전역 변수
        snippetsDir = file('build/generated-snippets')
    }

    test {
        outputs.dir snippetsDir
    }

    asciidoctor {
        inputs.dir snippetsDir
        configurations 'asciidoctorExt'

        sources { // 특정 파일만 html로 만든다.
            include("**/index.adoc")
        }
        baseDirFollowsSourceFile() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다.
        dependsOn test
    }

    bootJar {
        dependsOn asciidoctor
        from("${asciidoctor.outputDir}") {
            into 'static/docs'
        }
    }
}
